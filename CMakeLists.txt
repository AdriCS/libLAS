###############################################################################
# Main CMake configuration file for libLAS
#
# Author: Mateusz Loskot <mateusz@loskot.net>
#
# ************************************************************************
# WARNING (mloskot): A PROTOTYPE - WORK IN PROGRESS - FEEL FREE TO IMPROVE
# ************************************************************************
#
###############################################################################
# libLAS general settings
PROJECT(libLAS)

# Name of C++ library
SET(LIBLAS_LIB_NAME las)

# Name of C library
SET(LIBLAS_C_LIB_NAME las_c)

# Choose package components
SET(WITH_UTILITIES TRUE CACHE BOOL "Choose if libLAS utilities should be built")
SET(WITH_TESTS FALSE CACHE BOOL "Choose if libLAS unit tests should be built")

###############################################################################
# CMake settings
CMAKE_MINIMUM_REQUIRED(VERSION 2.6.0)

SET(CMAKE_COLOR_MAKEFILE ON)

# Allow advanced users to generate Makefiles printing detailed commands
MARK_AS_ADVANCED(CMAKE_VERBOSE_MAKEFILE)

# Path to additional CMake modules
SET(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/build/cmake ${CMAKE_MODULE_PATH})

###############################################################################
# Build type settings

IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE Debug CACHE STRING
        "Choose the type of build, options are: None Debug Release RelWithDebInfo MinSizeRel"
        FORCE)
ENDIF()

SET(BUILD_PEDANTIC TRUE CACHE BOOL "Choose compilation in pedantic or relaxed mode")

###############################################################################
# Platform and compiler specific settings

IF(WIN32)
    IF (MSVC)
        IF(BUILD_PEDANTIC)
            SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /W4")
        ENDIF()

        IF (MSVC80)
            ADD_DEFINITIONS(-D_CRT_SECURE_NO_WARNINGS)
            ADD_DEFINITIONS(-D_CRT_NONSTDC_NO_WARNING)
        ENDIF()
    ENDIF()
ELSE()
    IF(CMAKE_COMPILER_IS_GNUCC OR CMAKE_COMPILER_IS_GNUCXX)
        
        SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fPIC -Wall -Wno-long-long -ansi")

        IF(BUILD_PEDANTIC)
            SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -pedantic")
        ENDIF()
        
        IF (CMAKE_COMPILER_IS_GNUCXX)
            SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++98")
        ENDIF()
    ENDIF()
ENDIF(WIN32)

###############################################################################
# Search for dependencies

# Boost C++ Libraries support - optional, default=OFF
# TODO

# zlib support - optional, default=OFF
SET(WITH_ZLIB FALSE CACHE BOOL "Choose if zlib support should be built")

IF(WITH_ZLIB)
    FIND_PACKAGE(ZLIB)

    IF(ZLIB_FOUND)
        INCLUDE_DIRECTORIES(${ZLIB_INCLUDE_DIR})
        ADD_DEFINITIONS(-DHAVE_ZLIB=1)
    ENDIF()
ENDIF()

# GeoTIFF support - optional, default=OFF
SET(WITH_GEOTIFF FALSE CACHE BOOL "Choose if GeoTIFF support should be built")

IF(WITH_GEOTIFF)
    FIND_PACKAGE(GeoTIFF 1.2.5)

    IF(GEOTIFF_FOUND)
        INCLUDE_DIRECTORIES(${GEOTIFF_INCLUDE_DIR})
        ADD_DEFINITIONS(-DHAVE_LIBGEOTIFF=1)
    ENDIF()
ENDIF()

# GDAL/OGR support - optional, default=OFF
SET(WITH_GDAL FALSE CACHE BOOL "Choose if GDAL support should be built")

IF(WITH_GDAL)
    IF (NOT GEOTIFF_FOUND)
        MESSAGE(FATAL_ERROR "GDAL support requires GeoTIFF library which was not selected")
    ENDIF()

    FIND_PACKAGE(GDAL 1.6.0)

    IF(GDAL_FOUND)
        INCLUDE_DIRECTORIES(${GDAL_INCLUDE_DIR})
	    ADD_DEFINITIONS(-DHAVE_GDAL=1)
    ENDIF()
ENDIF()

# Spatial Index support - optional, default=OFF
SET(WITH_SPATIALINDEX FALSE CACHE BOOL "Choose if GeoTIFF support should be built")

IF(WITH_SPATIALINDEX)
    FIND_PACKAGE(SpatialIndex 1.4.0)

    IF(SPATIALINDEX_FOUND)
        INCLUDE_DIRECTORIES(${SPATIALINDEX_INCLUDE_DIR})
        ADD_DEFINITIONS(-DHAVE_SPATIALINDEX=1)
    ENDIF()
ENDIF()

# Oracle support - optional, default=OFF
# TODO

###############################################################################
# Installation settings

IF(WIN32)
    SET(DEFAULT_LIB_SUBDIR lib)
    SET(DEFAULT_DATA_SUBDIR .)
    SET(DEFAULT_INCLUDE_SUBDIR include)

    IF (MSVC)
        SET(DEFAULT_BIN_SUBDIR bin)
    ELSE()
        SET(DEFAULT_BIN_SUBDIR .)
    ENDIF()
ELSE()
    # Common locatoins for Unix and Mac OS X
    SET(DEFAULT_BIN_SUBDIR bin)
    SET(DEFAULT_LIB_SUBDIR lib)
    SET(DEFAULT_DATA_SUBDIR share/liblas)
    SET(DEFAULT_INCLUDE_SUBDIR include/liblas)
ENDIF()

# Locations are changeable by user to customize layout of libLAS installation
# (default values are platform-specific)
SET(LIBLAS_BIN_SUBDIR ${DEFAULT_BIN_SUBDIR} CACHE STRING
    "Subdirectory where executables will be installed")
SET(LIBLAS_LIB_SUBDIR ${DEFAULT_LIB_SUBDIR} CACHE STRING
    "Subdirectory where libraries will be installed")
SET(LIBLAS_INCLUDE_SUBDIR ${DEFAULT_INCLUDE_SUBDIR} CACHE STRING
    "Subdirectory where header files will be installed")
SET(LIBLAS_DATA_SUBDIR ${DEFAULT_DATA_SUBDIR} CACHE STRING
    "Subdirectory where data will be installed")

# Mark *_SUBDIR variables as advanced and dedicated to use by power-users only.
MARK_AS_ADVANCED(LIBLAS_BIN_SUBDIR LIBLAS_LIB_SUBDIR LIBLAS_INCLUDE_SUBDIR LIBLAS_DATA_SUBDIR)

# Full paths for the installation
SET(LIBLAS_BIN_DIR ${LIBLAS_BIN_SUBDIR})
SET(LIBLAS_LIB_DIR ${LIBLAS_LIB_SUBDIR})
SET(LIBLAS_INCLUDE_DIR ${LIBLAS_INCLUDE_SUBDIR})
SET(LIBLAS_DATA_DIR ${LIBLAS_DATA_SUBDIR})

###############################################################################
# Installation commands

INSTALL(FILES AUTHORS ChangeLog COPYING INSTALL LICENSE.txt README
    DESTINATION ${LIBLAS_DATA_DIR}/doc)

###############################################################################
# Processing of project directories

ADD_SUBDIRECTORY(src)

IF(WITH_UTILITIES)
    ADD_SUBDIRECTORY(apps)
ENDIF()

IF(WITH_TESTS)
    ADD_SUBDIRECTORY(test)
ENDIF()
